const LOCALID_WALLY        = 6
const LOCALID_WALLYS_UNCLE = 7
const LOCALID_SCOTT        = 11


script MauvilleCity_EventScript_Wally {
	lockall
	if (flag(FLAG_DECLINED_WALLY_BATTLE_MAUVILLE)){
		goto (MauvilleCity_EventScript_WallyRequestBattleAgain)
	}
	applymovement(LOCALID_WALLY, Common_Movement_WalkInPlaceFasterRight)
	waitmovement(0)
	namebox(Wally)
	msgbox(MauvilleCity_Text_WallyWantToChallengeGym, MSGBOX_DEFAULT)
	namebox(TioWally)
	msgbox(MauvilleCity_Text_UncleYourePushingIt, MSGBOX_DEFAULT)
	namebox(Wally)
	msgbox(MauvilleCity_Text_WallyWeCanBeatAnyone, MSGBOX_DEFAULT)
	closemessage
	hidenamebox
	applymovement(LOCALID_WALLY, Common_Movement_FacePlayer)
	waitmovement(0)
	playse (SE_PIN)
	applymovement(LOCALID_WALLY, Common_Movement_ExclamationMark)
	waitmovement(0)
	applymovement(LOCALID_WALLY, Common_Movement_Delay48)
	waitmovement(0)
	namebox(Wally)
	msgbox(MauvilleCity_Text_WallyWillYouBattleMe, MSGBOX_YESNO)
	goto(MauvilleCity_EventScript_BattleWallyPrompt)
	end
}
	

script MauvilleCity_EventScript_BattleWallyPrompt {
	if (var(VAR_RESULT) == YES){
		call (MauvilleCity_EventScript_BattleWally)
	}
	else {
		goto (MauvilleCity_EventScript_DeclineWallyBattle)
	}
	closemessage
	hidenamebox
	switch (var(VAR_FACING)) {
		case DIR_NORTH:
			goto (MauvilleCity_EventScript_WallyAndUncleExitNorth)
		case DIR_EAST:
			goto (MauvilleCity_EventScript_WallyAndUncleExitEast)
	}
	end
}

script MauvilleCity_EventScript_WallyAndUncleExitNorth {
	applymovement(OBJ_EVENT_ID_PLAYER, MauvilleCity_Movement_PlayerWatchWallyExitNorth1)
	applymovement(LOCALID_WALLY, MauvilleCity_Movement_WallyExitNorth1)
	applymovement(LOCALID_WALLYS_UNCLE, MauvilleCity_Movement_WallysUncleExitNorth1)
	waitmovement(0)
	applymovement(LOCALID_WALLY, Common_Movement_WalkInPlaceFasterRight)
	waitmovement(0)
	delay(30)
	namebox(Wally)
	msgbox(MauvilleCity_Text_WallyNoNeedTo, MSGBOX_DEFAULT)
	closemessage
	namebox(TioWally)
	msgbox(MauvilleCity_Text_WallysUncleItsOkay, MSGBOX_DEFAULT)
	closemessage
	hidenamebox
	applymovement(LOCALID_WALLY, MauvilleCity_Movement_WallyExitNorth2)
	waitmovement(0)
	delay(36)
	applymovement(OBJ_EVENT_ID_PLAYER, MauvilleCity_Movement_PlayerFaceUncleNorth)
	applymovement(LOCALID_WALLYS_UNCLE, MauvilleCity_Movement_WallysUncleApproachPlayerNorth)
	waitmovement(0)
	call (MauvilleCity_EventScript_StartScottScene)
	applymovement(OBJ_EVENT_ID_PLAYER, MauvilleCity_Movement_PlayerWatchWallyExitNorth2)
	applymovement(LOCALID_SCOTT, MauvilleCity_Movement_PlayerWatchWallyExitNorth2)
	applymovement(LOCALID_WALLYS_UNCLE, MauvilleCity_Movement_WallysUncleExitNorth2)
	waitmovement(0)
	goto(MauvilleCity_EventScript_DefeatedWally)
	end
}

script MauvilleCity_EventScript_WallyAndUncleExitEast {
	applymovement(OBJ_EVENT_ID_PLAYER, MauvilleCity_Movement_PlayerWatchWallyExitEast1)
	applymovement(LOCALID_WALLY, MauvilleCity_Movement_WallyExitEast1)
	applymovement(LOCALID_WALLYS_UNCLE, MauvilleCity_Movement_WallysUncleExitEast1)
	waitmovement(0)
	applymovement(LOCALID_WALLY, Common_Movement_WalkInPlaceFasterRight)
	waitmovement(0)
	delay(30)
	namebox(Wally)
	msgbox(MauvilleCity_Text_WallyNoNeedTo, MSGBOX_DEFAULT)
	closemessage
	namebox(TioWally)
	msgbox(MauvilleCity_Text_WallysUncleItsOkay, MSGBOX_DEFAULT)
	closemessage
	hidenamebox
	applymovement(LOCALID_WALLY, MauvilleCity_Movement_WallyExitEast2)
	waitmovement(0)
	delay(36)
	applymovement(LOCALID_WALLYS_UNCLE, MauvilleCity_Movement_WallysUncleApproachPlayerEast)
	waitmovement(0)
	call (MauvilleCity_EventScript_StartScottScene)
	applymovement(OBJ_EVENT_ID_PLAYER, MauvilleCity_Movement_PlayerWatchWallyExitEast2)
	applymovement(LOCALID_SCOTT, MauvilleCity_Movement_PlayerWatchWallyExitEast2)
	applymovement(LOCALID_WALLYS_UNCLE, MauvilleCity_Movement_WallysUncleExitEast2)
	waitmovement(0)
	goto(MauvilleCity_EventScript_DefeatedWally)
	end
}


script MauvilleCity_EventScript_StartScottScene {
	namebox(TioWally)
	msgbox(MauvilleCity_Text_UncleVisitUsSometime, MSGBOX_DEFAULT)
	closemessage
	hidenamebox
	switch (var(VAR_FACING)) {
		case DIR_NORTH:
			call (MauvilleCity_EventScript_ScottApproachPlayerNorth)
		case DIR_EAST:
			call (MauvilleCity_EventScript_ScottApproachPlayerEast)
	}
	applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkInPlaceFasterDown)
	applymovement(LOCALID_WALLYS_UNCLE, Common_Movement_WalkInPlaceFasterDown)
	waitmovement(0)
	namebox(Scott)
	msgbox(MauvilleCity_Text_ScottAParentShould, MSGBOX_DEFAULT)
	namebox(TioWally)
	msgbox(MauvilleCity_Text_AndWhoAreYou, MSGBOX_DEFAULT)
	namebox(Scott)
	msgbox(MauvilleCity_Text_ImScott, MSGBOX_DEFAULT)
	namebox(TioWally)
	msgbox(MauvilleCity_Text_AndWhyDoICare, MSGBOX_DEFAULT)
	namebox(Scott)
	msgbox(MauvilleCity_Text_ScottDefendsWally, MSGBOX_DEFAULT)
	namebox(TioWally)
	msgbox(MauvilleCity_Text_YouDontUnderstand, MSGBOX_DEFAULT)
	applymovement(LOCALID_WALLYS_UNCLE, Common_Movement_WalkInPlaceUp)
	waitmovement(0)
	applymovement(LOCALID_WALLYS_UNCLE, Common_Movement_WalkInPlaceLeft)
	waitmovement(0)
	namebox(TioWally)
	msgbox(MauvilleCity_Text_Although, MSGBOX_DEFAULT)
	closemessage
	hidenamebox
	addvar (VAR_SCOTT_STATE, 1)
	return
}

script MauvilleCity_EventScript_DefeatedWally {
	removeobject (LOCALID_WALLY)
	removeobject (LOCALID_WALLYS_UNCLE)
	clearflag (FLAG_HIDE_VERDANTURF_TOWN_WANDAS_HOUSE_WALLY)
	clearflag (FLAG_HIDE_VERDANTURF_TOWN_WANDAS_HOUSE_WALLYS_UNCLE)
	setflag (FLAG_DEFEATED_WALLY_MAUVILLE)
	setvar (VAR_WALLY_CALL_STEP_COUNTER, 0)
	setflag (FLAG_ENABLE_FIRST_WALLY_POKENAV_CALL)
	namebox(Scott)
	msgbox(MauvilleCity_Text_ScottBadFamily)
	applymovement(LOCALID_SCOTT, Common_Movement_WalkInPlaceUp)
	applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkInPlaceDown)
	waitmovement(0)
	msgbox(MauvilleCity_Text_ScottYoureTheRealDeal)
	closemessage
	hidenamebox
	switch (var(VAR_FACING)) {
		case DIR_NORTH:
			call (MauvilleCity_EventScript_ScottExitNorth)
		case DIR_EAST:
			call (MauvilleCity_EventScript_ScottExitEast)
	}
	removeobject (LOCALID_SCOTT)
	releaseall
	end
}

script MauvilleCity_EventScript_ScottApproachPlayerNorth {
	addobject (LOCALID_SCOTT)
	applymovement(LOCALID_SCOTT, MauvilleCity_Movement_ScottApproachPlayerNorth)
	waitmovement(0)
	return
}

script MauvilleCity_EventScript_ScottApproachPlayerEast {
	setobjectxyperm (LOCALID_SCOTT, 12, 13)
	addobject (LOCALID_SCOTT)
	applymovement(LOCALID_SCOTT, MauvilleCity_Movement_ScottApproachPlayerEast)
	waitmovement(0)
	return
}

script MauvilleCity_EventScript_ScottExitNorth {
	applymovement(OBJ_EVENT_ID_PLAYER, MauvilleCity_Movement_PlayerWatchScottExitNorth)
	applymovement(LOCALID_SCOTT, MauvilleCity_Movement_ScottExitNorth)
	waitmovement(0)
	return
}

script MauvilleCity_EventScript_ScottExitEast {
	applymovement(OBJ_EVENT_ID_PLAYER, MauvilleCity_Movement_PlayerWatchScottExitEast)
	applymovement(LOCALID_SCOTT, MauvilleCity_Movement_ScottExitEast)
	waitmovement(0)
	return
}

script MauvilleCity_EventScript_BattleWally {
	msgbox(MauvilleCity_Text_WallyHereICome, MSGBOX_DEFAULT)
	trainerbattle_no_intro (TRAINER_WALLY_MAUVILLE, MauvilleCity_Text_WallyDefeat)
	applymovement(LOCALID_WALLY, Common_Movement_WalkInPlaceFasterRight)
	waitmovement(0)
	namebox(Wally)
	msgbox(MauvilleCity_Text_WallyIllGoBackToVerdanturf, MSGBOX_DEFAULT)
	applymovement(LOCALID_WALLY, Common_Movement_FacePlayer)
	waitmovement(0)
	msgbox(MauvilleCity_Text_ThankYouNotEnoughToBattle, MSGBOX_DEFAULT)
	applymovement(LOCALID_WALLY, Common_Movement_WalkInPlaceFasterRight)
	waitmovement(0)
	namebox(TioWally)
	msgbox(MauvilleCity_Text_UncleNoNeedToBeDown, MSGBOX_DEFAULT)
	return
}

script MauvilleCity_EventScript_DeclineWallyBattle {
	setflag (FLAG_DECLINED_WALLY_BATTLE_MAUVILLE)
	namebox(Wally)
	msgbox(MauvilleCity_Text_WallyMyUncleWontKnowImStrong, MSGBOX_DEFAULT)
	hidenamebox
	closemessage
	release
	end
}

script MauvilleCity_EventScript_WallyRequestBattleAgain {
	applymovement(LOCALID_WALLY, Common_Movement_FacePlayer)
	waitmovement(0)
	msgbox(MauvilleCity_Text_WallyPleaseBattleMe, MSGBOX_YESNO)
	goto(MauvilleCity_EventScript_BattleWallyPrompt)
	end
}

movement MauvilleCity_Movement_WallyExitNorth1 {
	walk_left*2
	walk_down*2
	walk_left
	}

movement MauvilleCity_Movement_WallyExitEast1 {
	walk_down*2
	walk_left*3
	}

movement MauvilleCity_Movement_WallyExitNorth2 {
	delay_16*2
	walk_left*8
	delay_8
	}

movement MauvilleCity_Movement_WallyExitEast2 {
	delay_16*2
	walk_left*8
	delay_8
	}

movement MauvilleCity_Movement_PlayerWatchWallyExitNorth2 {
	delay_16
	delay_8
	walk_in_place_faster_left
	}

movement MauvilleCity_Movement_PlayerWatchWallyExitEast2 {
	delay_16*3
	walk_in_place_faster_left
	}

movement MauvilleCity_Movement_PlayerWatchScottExitNorth {
	delay_16
	walk_in_place_faster_left
	}

movement MauvilleCity_Movement_PlayerWatchScottExitEast {
	delay_16*2
	walk_in_place_faster_left
	}

movement MauvilleCity_Movement_PlayerWatchWallyExitEast1 {
	delay_16
	walk_in_place_faster_down
	}

movement MauvilleCity_Movement_PlayerWatchWallyExitNorth1 {
	delay_16
	walk_in_place_faster_left
	}

movement MauvilleCity_Movement_WallysUncleExitNorth1 {
	walk_left*3
	walk_down*2
	walk_in_place_faster_left
	}

movement MauvilleCity_Movement_WallysUncleExitEast1 {
	walk_left
	walk_down*2
	walk_left*2
	}

movement MauvilleCity_Movement_PlayerFaceUncleNorth {
	delay_16
	delay_8
	delay_4
	walk_in_place_faster_down
	}

movement MauvilleCity_Movement_WallysUncleApproachPlayerNorth {
	walk_right*2
	walk_in_place_faster_up
	}

movement MauvilleCity_Movement_WallysUncleApproachPlayerEast {
	walk_right
	walk_up
	}

movement MauvilleCity_Movement_WallysUncleExitNorth2 {
	walk_left*8
	}

movement MauvilleCity_Movement_WallysUncleExitEast2 {
	walk_left
	walk_down
	walk_left*7
	}

movement MauvilleCity_Movement_ScottApproachPlayerNorth {
	walk_up*5
	walk_left*4
	walk_in_place_faster_up
	}

movement MauvilleCity_Movement_ScottApproachPlayerEast {
	walk_up*5
	walk_left*5
	walk_in_place_faster_up
	}

movement MauvilleCity_Movement_ScottExitNorth {
	walk_left*9
	}

movement MauvilleCity_Movement_ScottExitEast {
	walk_down
	walk_left*9
	}


raw `
.set LOCALID_WALLY, 6
.set LOCALID_WALLYS_UNCLE, 7
.set LOCALID_SCOTT, 11

MauvilleCity_MapScripts::
	map_script MAP_SCRIPT_ON_TRANSITION, MauvilleCity_OnTransition
	.byte 0

MauvilleCity_OnTransition:
	setflag FLAG_VISITED_MAUVILLE_CITY
	clearflag FLAG_FORCE_MIRAGE_TOWER_VISIBLE
	clearflag FLAG_HIDE_SLATEPORT_MUSEUM_POPULATION
	setflag FLAG_SYS_TV_START
	clearflag FLAG_MAUVILLE_GYM_BARRIERS_STATE
	setvar VAR_MAUVILLE_GYM_STATE, 0
	call_if_set FLAG_GOT_TM24_FROM_WATTSON, MauvilleCity_EventScript_MoveWattsonBackToGym
	end

MauvilleCity_EventScript_MoveWattsonBackToGym::
	clearflag FLAG_HIDE_MAUVILLE_GYM_WATTSON
	setflag FLAG_HIDE_MAUVILLE_CITY_WATTSON
	setflag FLAG_WATTSON_REMATCH_AVAILABLE
	return

MauvilleCity_EventScript_Boy::
	msgbox MauvilleCity_Text_NurseHurtMonBackToHealth, MSGBOX_NPC
	end

MauvilleCity_EventScript_Maniac::
	msgbox MauvilleCity_Text_AllSortsOfPeopleComeThrough, MSGBOX_NPC
	end

MauvilleCity_EventScript_Woman::
	msgbox MauvilleCity_Text_RydelVeryGenerous, MSGBOX_NPC
	end

MauvilleCity_EventScript_RichBoy::
	msgbox MauvilleCity_Text_PokemonCanJumpYouOnBike, MSGBOX_NPC
	end

MauvilleCity_EventScript_CitySign::
	msgbox MauvilleCity_Text_CitySign, MSGBOX_SIGN
	end

MauvilleCity_EventScript_GymSign::
	msgbox MauvilleCity_Text_GymSign, MSGBOX_SIGN
	end

MauvilleCity_EventScript_BikeShopSign::
	msgbox MauvilleCity_Text_BikeShopSign, MSGBOX_SIGN
	end

MauvilleCity_EventScript_GameCornerSign::
	msgbox MauvilleCity_Text_GameCornerSign, MSGBOX_SIGN
	end

MauvilleCity_EventScript_SchoolKidM::
	lock
	faceplayer
	goto_if_set FLAG_TV_EXPLAINED, MauvilleCity_EventScript_TVExplained
	msgbox MauvilleCity_Text_ExplainTV, MSGBOX_DEFAULT
	setflag FLAG_TV_EXPLAINED
	release
	end

MauvilleCity_EventScript_TVExplained::
	msgbox MauvilleCity_Text_BeenCheckingOutTV, MSGBOX_DEFAULT
	release
	end

MauvilleCity_EventScript_WallysUncle::
	lock
	faceplayer
	goto_if_set FLAG_DECLINED_WALLY_BATTLE_MAUVILLE, MauvilleCity_EventScript_UncleAskPlayerToBattleWally
	namebox TioWally
	msgbox MauvilleCity_Text_UncleHesTooPeppy, MSGBOX_DEFAULT
	closemessage
	hidenamebox
	applymovement LOCALID_WALLYS_UNCLE, Common_Movement_FaceOriginalDirection
	waitmovement 0
	release
	end

MauvilleCity_EventScript_UncleAskPlayerToBattleWally::
	namebox TioWally
	msgbox MauvilleCity_Text_UncleCanYouBattleWally, MSGBOX_DEFAULT
	closemessage
	hidenamebox
	applymovement LOCALID_WALLYS_UNCLE, Common_Movement_FaceOriginalDirection
	waitmovement 0
	release
	end


MauvilleCity_EventScript_Wattson::
	lock
	faceplayer
	checkitem ITEM_TM55
	goto_if_eq VAR_RESULT, TRUE, MauvilleCity_EventScript_ReceivedThunderbolt
	goto_if_eq VAR_NEW_MAUVILLE_STATE, 2, MauvilleCity_EventScript_CompletedNewMauville
	goto_if_set FLAG_GOT_BASEMENT_KEY_FROM_WATTSON, MauvilleCity_EventScript_BegunNewMauville
	msgbox MauvilleCity_Text_WattsonNeedFavorTakeKey, MSGBOX_DEFAULT
	giveitem ITEM_BASEMENT_KEY
	setflag FLAG_GOT_BASEMENT_KEY_FROM_WATTSON
	msgbox MauvilleCity_Text_WattsonWontBeChallenge, MSGBOX_DEFAULT
	release
	end

MauvilleCity_EventScript_BegunNewMauville::
	msgbox MauvilleCity_Text_WattsonWontBeChallenge, MSGBOX_DEFAULT
	release
	end

MauvilleCity_EventScript_CompletedNewMauville::
	msgbox MauvilleCity_Text_WattsonThanksTakeTM, MSGBOX_DEFAULT
	giveitem ITEM_TM55
	goto_if_eq VAR_RESULT, FALSE, Common_EventScript_ShowBagIsFull
	msgbox MauvilleCity_Text_WattsonYoungTakeCharge, MSGBOX_DEFAULT
	release
	end

MauvilleCity_EventScript_ReceivedThunderbolt::
	msgbox MauvilleCity_Text_WattsonYoungTakeCharge, MSGBOX_DEFAULT
	release
	end

MauvilleCity_EventScript_RegisterWallyCall::
	lockall
	pokenavcall MauvilleCity_Text_WallyPokenavCall
	waitmessage
	delay 30
	playfanfare MUS_REGISTER_MATCH_CALL
	msgbox MauvilleCity_Text_RegisteredWally, MSGBOX_DEFAULT
	waitfanfare
	closemessage
	delay 30
	setflag FLAG_ENABLE_WALLY_MATCH_CALL
	clearflag FLAG_ENABLE_FIRST_WALLY_POKENAV_CALL
	releaseall
	end

MauvilleCity_Text_WattsonNeedFavorTakeKey:
	.string "Ah, {PAUSE 2}¡{PLAYER}!\p"
	.string "¡Que alegría verte aquí!\n"
	.string "¡Hasta se ve que estás hechando\l"
	.string "chispas, BUAJAJAJAJA!\p"
	.string "¡Mira, mira, {PLAYER}, necesito que me\n"
	.string "hagas un favor.\p"
	.string "Hace tiempo, creé un lugar llamado\n"
	.string "Nueva Mauville, pero por alguna razón,\l"
	.string "el generador ha vuelto a encenderse…\l"
	.string "¡Y eso molesta a los Pokémon salvajes,\l"
	.string "podría sobrecargarlos y causar\l"
	.string "aprietos!\p"
	.string "Necesito que vayas ahí y apagues el\n"
	.string "generador. No puedo hacerlo yo porque\l"
	.string "mis piernas ya no son lo que eran…\l"
	.string "Así que toma estas Llaves, y apaga ese\l"
	.string "generador por mí,{PAUSE 2} ¿Okay?$"

MauvilleCity_Text_WattsonWontBeChallenge:
	.string "¡Ah, y no te preocupes!\n"
	.string "Ese lugar es inseguro, pero no mortal.\l"
	.string "Estarás bien, {PAUSE 2}¡No es como si un humano\l"
	.string "pudiera sobrecargarse de electricidad\l"
	.string "como un Voltorb, BUAJAJAJAJA!$"

MauvilleCity_Text_WattsonThanksTakeTM:
	.string "¡BUAJAJAJAJA!\p"
	.string "¡Sabía que podrías!\p"
	.string "¡Ahora, ten esta MT.\n"
	.string "Contiene el movimiento Rayo Carga, que\l"
	.string "subirá tu Ataque Especial en un nivel\l"
	.string "después de usarse, {PAUSE 2}¿Muy útil, no?\p"
	.string "¡Se podría decir que cargas con un arma\n"
	.string "ahora, BUAJAJAJAJAJA!$"

MauvilleCity_Text_WattsonYoungTakeCharge:
	.string "¡Se podría decir que cargas con un arma\n"
	.string "ahora, BUAJAJAJAJAJA!$"

MauvilleCity_Text_NurseHurtMonBackToHealth:
	.string "You know, it's cool to have POKéMON\n"
	.string "battles and stuff…\p"
	.string "But if your POKéMON gets hurt,\n"
	.string "you have to nurse it back to health.$"

MauvilleCity_Text_AllSortsOfPeopleComeThrough:
	.string "The roads of this town stretch north\n"
	.string "and south, and east and west.\p"
	.string "Because of that, we get all sorts of\n"
	.string "people coming through.$"

MauvilleCity_Text_RydelVeryGenerous:
	.string "Have you been to RYDEL'S CYCLES yet?\p"
	.string "RYDEL, the owner, is a very generous\n"
	.string "man.$"

MauvilleCity_Text_PokemonCanJumpYouOnBike:
	.string "Even if you're riding a BIKE,\n"
	.string "wild POKéMON could jump you.$"

MauvilleCity_Text_CitySign:
	.string "MAUVILLE CITY\n"
	.string "“The bright and shiny city of fun!”$"

MauvilleCity_Text_GymSign:
	.string "MAUVILLE CITY POKéMON GYM\n"
	.string "LEADER: WATTSON\l"
	.string "“The cheerfully electrifying man!”$"

MauvilleCity_Text_BikeShopSign:
	.string "“Ride in gravel and shake up your\n"
	.string "soul!”\l"
	.string "RYDEL'S CYCLES$"

MauvilleCity_Text_GameCornerSign:
	.string "“The play spot for all!”\n"
	.string "MAUVILLE GAME CORNER$"

MauvilleCity_Text_ExplainTV:
	.string "Hi, do you check out TV at all?\p"
	.string "They've added a bunch of cool new\n"
	.string "shows recently.\p"
	.string "TV is so cool. I've seen my friends on\n"
	.string "TV just out of the blue.\p"
	.string "And, sometimes, they even put you on TV\n"
	.string "without telling you.\p"
	.string "I wish I would be on TV sometime.\p"
	.string "That's why I think you should check\n"
	.string "out TVs whenever you can.$"

MauvilleCity_Text_BeenCheckingOutTV:
	.string "Hi, have you been checking out TVs?$"
`
